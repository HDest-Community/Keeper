version "4.0"

class KeeperHandler : EventHandler
{
	override void PlayerDied(PlayerEvent e)
	{
		let plr = HDPlayerPawn(players[e.PlayerNumber].mo);
		if (plr)
		{
			let Restorer = StatRestorer(plr.GiveInventoryType('StatRestorer'));
			if (Restorer)
			{
				double Percent = clamp(keeper_percent, 0, 1.0);
				Restorer.Blues = int(plr.regenblues * Percent);
				Restorer.Aggro = int(plr.aggravateddamage * Percent);
				Restorer.Burns = int(plr.burncount * Percent);
			}
		}
	}
}

// [Ace] Defer restoring stats a few tics later otherwise the player's OnRespawn virtual will clear everything you do in PlayerRespawned.
class StatRestorer : Inventory
{
	override void DoEffect()
	{
		if (Health > 0 && ++AliveTicker > 2)
		{
			let plr = HDPlayerPawn(owner);
			plr.regenblues = Blues;
			plr.aggravateddamage = Aggro;
			plr.burncount =  Burns;

			Destroy();
			return;
		}

		Super.DoEffect();
	}

	int AliveTicker;

	int Blues;
	int Aggro;
	int Burns;

	Default
	{
		Inventory.MaxAmount 1;
	}
}